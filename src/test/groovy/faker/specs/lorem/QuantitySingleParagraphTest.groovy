package faker.specs.lorem

import spock.lang.Specification
import faker.Lorem
import faker.specs.support.Localized

@Localized
class QuantitySingleParagraphTest extends Specification {

    def "paragraph, no number of sentences"(def p) {
        expect: "the paragraphs should have 3-6 sentences"
        assert p.split('\\. ').size() in 3..6

        where: "the paragraph is generated by Lorem.paragraph()"
        p << (1..100).collect { Lorem.paragraph() }
    }

    def "paragraph, exact number of paragraphs"(def p) {
        expect: "the paragraphs should have exactly 5 sentences"
        assert p.split('\\. ').size() == 5

        where: "the paragraph is generated by Lorem.paragraph(5)"
        p << (1..100).collect { Lorem.paragraph(5) }
    }

    def "paragraph, range number of paragraphs"(def p) {
        expect: "the paragraphs should have 1..3 sentences"
        assert p.split('\\. ').size() in 1..3

        where: "the paragraph is generated by Lorem.paragraph(1..3)"
        p << (1..100).collect { Lorem.paragraph(1..3) }
    }

    def "paragraph, list number of paragraphs"(def p) {
        expect: "the paragraphs should have 1 or 4 sentences"
        assert p.split('\\. ').size() in [1, 4]

        where: "the paragraph is generated by Lorem.paragraph([1, 4])"
        p << (1..100).collect { Lorem.paragraph([1, 4]) }
    }
}